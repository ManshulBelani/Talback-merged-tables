apply from: 'base.gradle'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

android {
    sourceSets {
        aosp {
            java.srcDirs = ["$projectDir/src/aosp/java"]
            res.srcDirs = ["$projectDir/src/aosp/res"]
        }
    }
    productFlavors {
        aosp {
            applicationId "com.android.talkback"
            testApplicationId "com.android.talkback.test"
            versionName "5.0.7_aosp"
        }
    }
    buildToolsVersion '25.0.2'
}

dependencies {
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.android.support:support-v4:24.0.0'
}

task grantPermission(type: Exec) {
    commandLine 'adb', 'shell', 'pm', 'grant', 'com.android.talkback.test',
            'android.permission.WRITE_SECURE_SETTINGS'
}

task instrumentationTest(type: Exec, dependsOn: ['installAospDebug',
                                                 'installAospDebugAndroidTest',
                                                 'grantPermission']) {
    commandLine 'adb', 'shell', 'am', 'instrument', '-w',
            'com.android.talkback.test/android.test.InstrumentationTestRunner'
}

afterEvaluate {
    tasks.withType (org.gradle.api.tasks.compile.AbstractCompile) {
        compileTask -> compileTask.dependsOn(copyPreferencesXmlForV21)
    }
}
