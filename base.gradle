apply plugin: 'com.android.application'

def projectDir = System.getProperty('projectPath')
def rootDir = "$projectDir/../../.."

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

repositories {
    mavenCentral()
    maven {
        // TODO: Remove this after support library v24 public release.
        url "$rootDir/prebuilts/fullsdk/extras/android/m2repository"
    }
}

android {
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
    }
    compileSdkVersion "android-24"
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),    \
               "$projectDir/src/main/proguard-project.txt"
            signingConfig signingConfigs.debug
        }
    }
    signingConfigs {
        debug {
            storeFile file("$projectDir/cert/debug.keystore")
            storePassword "android"
            keyAlias "debug"
        }
    }
    sourceSets {
        main {
            setRoot("$projectDir/src/main")
        }
        test {
            setRoot("$projectDir/src/test")
        }
        androidTest {
            setRoot("$projectDir/src/instrumentation_tests")
            android.sourceSets.main.res.srcDirs = ["$projectDir/src/main/res",
                                                   "$projectDir/src/instrumentation_tests/res"]
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
}

dependencies {
    testCompile 'junit:junit:4.12'

    testCompile('org.robolectric:robolectric:3.0') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
        exclude module: 'support-v4'
    }
    testCompile('org.mockito:mockito-core:1.10.8') {
        exclude module: 'hamcrest-core'
    }

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile('com.squareup.dagger:dagger:1.1.0') {
        exclude module: 'junit'
    }

    androidTestCompile('com.squareup:fest-android:1.0.8') {
        exclude module: 'support-v4'
    }
    androidTestCompile('org.mockito:mockito-core:1.10.8') {
        exclude module: 'hamcrest-core'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

// Copy preferences.xml to xml-v21 replacing CheckBoxPreference with SwitchPreference.
task copyPreferencesXmlForV21(type: Copy) {
    from "$projectDir/src/main/res/xml/preferences.xml"
    into "$projectDir/src/main/res/xml-v21/"
    filter { String l -> l.replaceAll("CheckBoxPreference", "SwitchPreference") }
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--multi-dex'
    }
}
